plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.1'
}


import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import org.gradle.internal.os.OperatingSystem
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

ext {
    if (project.hasProperty('hytale_home')) {
        hytaleHome = project.findProperty('hytale_home')
    }
    else {
        def os = OperatingSystem.current()
        if (os.isWindows()) {
            hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
        }
        else if (os.isMacOsX()) {
            hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
        }
        else if (os.isLinux()) {
            hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
            if (!file(hytaleHome).exists()) {
                hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
            }
        }
    }
}

if (!project.hasProperty('hytaleHome')) {
    throw new GradleException('Your Hytale install could not be detected automatically. If you are on an unsupported platform or using a custom install location, please define the install location using the hytale_home property.');
}
else if (!file(project.findProperty('hytaleHome')).exists()) {
    throw new GradleException("Failed to find Hytale at the expected location. Please make sure you have installed the game. The expected location can be changed using the hytale_home property. Currently looking in ${project.findProperty('hytaleHome')}")
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code. This requires you to have Hytale installed using
// the official launcher for now.
configurations {
    shade
}
dependencies {
    // Provided by the server at runtime; needed for compilation + IDE run only
    compileOnly("com.hypixel.hytale:Server:+")
    runtimeOnly("com.hypixel.hytale:Server:+")
    // Only put things you actually want inside the fat jar here:
    shade("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    // shade("group:artifact:version") ...
}

tasks.named("shadowJar") {
    configurations = [project.configurations.shade]
    archiveClassifier.set("")
    minimize()
    exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA"
}

// Ensure `build` produces the shaded jar, and avoid producing a confusing thin jar.
tasks.named('build') {
    dependsOn(tasks.named('shadowJar'))
}

tasks.named('jar') {
    enabled = false
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Updates the manifest.json file with the latest properties defined in the
// build.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(manifestJson))
    }
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

def createServerRunArguments(String srcDir) {
    def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/$patchline/package/game/latest/Assets.zip" + '"'
    def modPaths = []
    if (includes_pack.toBoolean()) {
        modPaths << srcDir
    }
    if (load_user_mods.toBoolean()) {
        modPaths << "${hytaleHome}/UserData/Mods"
    }
    if (!modPaths.isEmpty()) {
        programParameters += ' --mods="' + modPaths.join(',') + '"'
    }
    return programParameters
}

// Creates a run configuration in IDEA that will run the Hytale server with
// your plugin and the default assets.
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = createServerRunArguments(sourceSets.main.java.srcDirs.first().parentFile.absolutePath)
        workingDirectory = serverRunDir.absolutePath
    }
}

// Creates a launch.json file for VSCode with the same configuration
tasks.register('generateVSCodeLaunch') {
    def vscodeDir = file("$projectDir/.vscode")
    def launchFile = file("$vscodeDir/launch.json")
    doLast {
        if (!vscodeDir.exists()) {
            vscodeDir.mkdirs()
        }
        def programParams = createServerRunArguments("\${workspaceFolder}")
        def launchConfig = [
                version: "0.2.0",
                configurations: [
                        [
                                type: "java",
                                name: "HytaleServer",
                                request: "launch",
                                mainClass: "com.hypixel.hytale.Main",
                                args: programParams,
                                cwd: "\${workspaceFolder}/run"
                        ]
                ]
        ]
        launchFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(launchConfig))
    }
}
repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "hytale-release"
        url = uri("https://maven.hytale.com/release")
    }
    maven {
        name = "hytale-pre-release"
        url = uri("https://maven.hytale.com/pre-release")
    }
}
kotlin {
    jvmToolchain(25)

    compilerOptions {
        jvmTarget = JvmTarget.JVM_24
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            // Don't publish the thin jar coming from components.java
            // (it won't exist because jar is disabled).
            // from components.java

            // Publish the shaded jar as the main artifact:
            artifact(tasks.named("shadowJar"))

            // Keep these if you want custom coordinates:
            groupId = 'com.trinex'
            artifactId = 'TrinexLib'
        }
    }
}
tasks.named("publishMavenJavaPublicationToMavenLocal") {
    dependsOn(tasks.named("shadowJar"))
}

// Ensure Java compilation also targets 24 bytecode to match Kotlin
tasks.withType(JavaCompile).configureEach {
    options.release.set(24)
}
